// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array bitPower;
    /** Initializes the library. */
    function void init() {
        let bitPower = Array.new(16);
        let bitPower[0] = 1;
        let bitPower[1] = 2;
        let bitPower[2] = 4;
        let bitPower[3] = 8;        
        let bitPower[4] = 16;
        let bitPower[5] = 32;        
        let bitPower[6] = 64;
        let bitPower[7] = 128;        
        let bitPower[8] = 256;
        let bitPower[9] = 512;        
        let bitPower[10] = 1024;
        let bitPower[11] = 2048;
        let bitPower[12] = 4096;
        let bitPower[13] = 8192;
        let bitPower[14] = 16384;
        let bitPower[15] = -32767-1;
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        }
        else {
            return x;
        }
    }
    
    /** Returns the ith bit of a number*/
    function boolean bit (int x, int i) {
        return ~((x & bitPower[i]) = 0);
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int result;
        var int shifted;
        var int i;
        let i = 0;
        let shifted = x;
        let result = 0;
        while (i < 16) {
            if (Math.bit(y,i)) {
                let result = result + shifted;
            }
            let shifted = shifted + shifted;
            let i = i + 1;
        }
        return result;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int temp;
        if ((x>0) = (y<0)) {
            return -(Math.divide(Math.abs(x),Math.abs(y)));
        }
        if (x < y) {
            return 0;
        }
        let temp = Math.divide(x,y+y);
        let temp = temp + temp;
        if ((x-y)<(temp*y)) {
            return temp;
        }
            return temp+1;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int i;
        var int y;
        var int temp;
        let i = 7;
        let y = 0;
        while (i > -1 ) {
            let temp = y+bitPower[i];
            if ( (~((temp*temp) > x))&(temp < 182)) {
                let y = temp;
            }
            let i = i - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a>b) {
            return a;
        }
        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a<b) {
            return a;
        }
        return b;
    }
}
