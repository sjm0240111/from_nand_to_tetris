class Main {
    function void main() {
        var Board board;
        var int key, moves;
        var boolean won, ready, moved;

        let ready = false;
        let key = 0;

        while (~ready) {
            // print explaination
            do Output.moveCursor(9, 18);
            do Output.printString("Use arrow key to move numbers,");
            do Output.moveCursor(11, 16);
            do Output.printString("until they appear in numeric order.");
            do Output.moveCursor(13, 19);
            do Output.printString("Press any key to start play");
            while (key = 0) {
                let key = Keyboard.keyPressed();
                let ready = true;
            }
        }

        do Screen.clearScreen();
        let key = 0;

        // initial board and parameters
        let board = Board.new();
        let won = false;
        let moved = false;
        let moves = 0;

        while (~won) {
            let won = board.won();
            // print moves
            do Output.moveCursor(0, 0);
            do Output.printString("moves: ");
            do Output.moveCursor(0, 8);
            do Output.printInt(moves);

            // draw numbers
            do board.draw();

            // wait for user press key
            while (key = 0) {
                let key = Keyboard.keyPressed();
            }
            // move if not won and advance moves
            if (~won) {
                // user pressed left
                if (key = 130) {
                    let moved = board.moveLeft();
                }
                // user pressed up
                if (key = 131) {
                    let moved = board.moveUp();
                }
                // user pressed right
                if (key = 132) {
                    let moved = board.moveRight();
                }
                // user pressed down
                if (key = 133) {
                    let moved = board.moveDown();
                }

                if (moved) {
                    let moves = moves + 1;
                }

                // print new state
                do Sys.wait(300);
                do Screen.clearScreen();
                do Output.moveCursor(0, 0);
                let moved = false;
                let key = 0;
            }
        }

        // end of game
        do Output.moveCursor(18, 27);
        do Output.printString("You WIN!");
        do Sys.wait(500);

        return;
    }
}
